module Execute_Cycle (CLK, Rst, RegWriteE, ALUSrcE, MemWriteE, ResultSrcE, BranchE,
 ALUControlE, RD1_E, RD2_E, Imm_Ext_E, RD_E, PCE, PCPlus4E, PCSrcE, PCTargetE, 
RegWriteM, MemWriteM, ResultSrcM, RD_M, PCPlus4M, WriteDataM, ALU_ResultM );
	
	//Declare in/out
	input CLK, Rst;
	input RegWriteE, ALUSrcE, MemWriteE, ResultSrcE, BranchE;
	input [2:0] ALUControlE;
	input [31:0] RD1_E, RD2_E, Imm_Ext_E, PCE, PCPlus4E;
	input [4:0] RD_E;
	
	
	output PCSrcE, RegWriteM, MemWriteM, ResultSrcM;
	output [31:0] PCTargetE, PCPlus4M, WriteDataM, ALU_ResultM;
	output [4:0] RD_M; 
	
	//Declare Wires
	wire [31:0] Src_B, ALUResultE;
	wire ZeroE;
	
	//Declare Reg
	reg RegWriteE_r, MemWriteE_r, ResultSrcE_r;
	reg [4:0] RD_E_r;
	reg [31:0] PCPlus4E_r, RD2_E_r, ALUResultE_r;
	
	//Init module
	//Declare alu
	ALU alu (.A(RD1_E), 
				.B(Src_B), 
				.Result(ALUResultE), 
				.ALUControl(ALUControlE), 
				.Zero(ZeroE)); 
	
	//Declare Mux
	Mux  MUX (.D0(RD2_E), 
		  .D1(Imm_Ext_E), 
		  .S(ALUSrcE),
		  .Y(Src_B));
	
	//Declare Adder
	PC_Adder Adder (.A(PCE), 
				 .B(Imm_Ext_E), 
				 .Result(PCTargetE));

	
	//Register logic
	always @(posedge CLK or negedge Rst) begin
		if(Rst == 0) begin
			RegWriteE_r <= 0;
			MemWriteE_r <= 0;
			ResultSrcE_r <=0;
			RD_E_r <= 0;
			PCPlus4E_r <= 0;
			RD2_E_r <= 0;
			ALUResultE_r <= 0;
		end
		else begin
			RegWriteE_r <= RegWriteE;
			MemWriteE_r <= MemWriteE;
			ResultSrcE_r <= ResultSrcE;
			RD_E_r <= RD_E;
			PCPlus4E_r <= PCPlus4E;
			RD2_E_r <= RD2_E;
			ALUResultE_r <= ALUResultE;
		end
	end
	
	
	// Output Assign
	assign PCSrcE = ZeroE&BranchE;
	assign RegWriteM = RegWriteE_r;
	assign MemWriteM = MemWriteE_r;
	assign ResultSrcM = ResultSrcE_r;
	assign RD_M = RD_E_r;
	assign PCPlus4M = PCPlus4E_r;
	assign WriteDataM = RD2_E_r;
	assign ALU_ResultM = ALUResultE_r;	
	
endmodule 